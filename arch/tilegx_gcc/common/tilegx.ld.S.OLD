#include <linux/kconfig.h> // Added by ertl-liyixiao
#include <asm-generic/vmlinux.lds.h>
#include <asm/page.h>
#include <asm/cache.h>
#include <asm/thread_info.h>
#include <hv/hypervisor.h>

/* Added by ertl-liyixiao */
#if 0
#define KERNEL_HIGH_VADDR	_AC(0x0000000f00000000, UL)  /* high 32GB */
#define MEM_SV_START		(KERNEL_HIGH_VADDR - 0x01000000)
#endif

/* Text loads starting from the supervisor interrupt vector address. */
#define TEXT_OFFSET MEM_SV_START

OUTPUT_ARCH(tile)
ENTRY(_start)

/* jiffies = jiffies_64; */

PHDRS
{
  intrpt PT_LOAD ;
  text PT_LOAD ;
  data PT_LOAD ;
}
SECTIONS
{
  /* Text is loaded with a different VA than data; start with text. */
  #undef LOAD_OFFSET
  #define LOAD_OFFSET TEXT_OFFSET
#define LOAD_OFFSET (0) /* P=V mode -- ertl-liyixiao */

  /* Hypervisor call vectors */
  .hvglue (HV_GLUE_START_CPA): AT (ADDR(.hvglue) - LOAD_OFFSET) {
    *(.hvglue)
  } :NONE

  . = ALIGN(0x20000);
  /* Interrupt vectors */
  .intrpt : /* AT ( 0 ) */  /* put at the start of physical memory */
  {
    _text = .;
    *(.intrpt)
  } :intrpt =0

  /* Now the real code */
  _stext = .;
  .text : AT (ADDR(.text) - LOAD_OFFSET) {
    HEAD_TEXT
    SCHED_TEXT
    LOCK_TEXT
    KPROBES_TEXT
    IRQENTRY_TEXT
    __fix_text_end = .;   /* tile-cpack won't rearrange before this */
    ALIGN_FUNCTION();
    *(.hottext*)
    TEXT_TEXT
    *(.text.*)
    *(.coldtext*)
    *(.fixup)
    *(.gnu.warning)
  } :text =0
  _etext = .;

  /* "Init" is divided into two areas with very different virtual addresses. */
  INIT_TEXT_SECTION(PAGE_SIZE)

  /* Now we skip back to PAGE_OFFSET for the data. */
  /*. = (. - TEXT_OFFSET + PAGE_OFFSET); */
  #undef LOAD_OFFSET
  #define LOAD_OFFSET PAGE_OFFSET
#define LOAD_OFFSET (0) /* P=V mode -- ertl-liyixiao */

  . = ALIGN(PAGE_SIZE);
  __init_begin = .;
  VMLINUX_SYMBOL(_sinitdata) = .;
  INIT_DATA_SECTION(16) :data =0
  PERCPU_SECTION(L2_CACHE_BYTES)
  . = ALIGN(PAGE_SIZE);
  VMLINUX_SYMBOL(_einitdata) = .;
  __init_end = .;

  _sdata = .;                   /* Start of data section */
  RO_DATA_SECTION(PAGE_SIZE)
  RW_DATA_SECTION(L2_CACHE_BYTES, PAGE_SIZE, THREAD_SIZE)
  _edata = .;

  EXCEPTION_TABLE(L2_CACHE_BYTES)
  NOTES

#ifdef CONFIG_FEEDBACK_COLLECT
  .feedback : {
    *(.feedback.start)
    __feedback_functions_start = .;
    *(.feedback.functions)
    __feedback_functions_end = .;
    *(.feedback.strings)
    *(.feedback.data)
    __feedback_section_end = .;
  }
#endif

  BSS_SECTION(8, PAGE_SIZE, 1)
  _end = . ;

  STABS_DEBUG
  DWARF_DEBUG

  DISCARDS
}
